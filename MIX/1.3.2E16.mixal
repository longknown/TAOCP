* compute and print rounded harmony numbers S2, S3, S4, S5
* procedure:
*   A. Start with mh = 1, S = 1;
*   B. Set me = mh + 1 and calculate r(n)(1/me) = r;
*   C. Find mh, the largest m for which r(n)(1/m) = r;
*   D. Add (mh - me + 1) * r to S and return to Step B.
*   largest m follows:
*       10^n/(r+1/2) < m <= 10^n(r-1/2)
*       ==> so compute floor((2 * 10^n) / (2*r -1))

* rI1: n index of Sn; rI2: m index of rn(m)
* !!!NOICE: m index can exceed the size of rI2, which is 4096
*       so we have to store it in memory and manipulate with rA & rX
SUM         EQU     500             store Sn in SUM
EXP10       EQU     SUM+1
VAR         EQU     SUM+2
VAR1        EQU     SUM+3
M           EQU     SUM+4
TERM        EQU     19
BLOCK       EQU     14

            ORIG    0
START       ENT1    1
            ENTA    1
            STA     EXP10
LOOP        ENT2    1
            ST2     M
            LDA     EXP10
            MUL     =10=
            STX     EXP10
            STX     SUM
1H          LDA     M
            INCA    1
            STA     M

* compute floor(10^n / m + 1/2), here thru (2 * 10^n + m) / (2*m)
            ADD     EXP10
            ADD     EXP10
            SRC     5
            LDA     M
            ADD     M               now rA holds 2*m
            STA     VAR
            ENTA    0
            DIV     VAR             now, rA contains rn(1/m), noted as 'r' (in short)
            JAZ     PRINT           if r == 0, print Sn and continue to S(n+1)
            STA     VAR             store r
            ADD     VAR
            DECA    1
            STA     VAR1            rA holds (2r-1)
            LDA     EXP10
            ADD     EXP10
            SRAX    5               shift rA to rX, and clear rA to 0
            DIV     VAR1            rA now holds the largest m for which r(n)(1/m) = r
            STA     VAR1
            SUB     M
            INCA    1
            MUL     VAR
            SRC     5
            ADD     SUM
            STA     SUM
            LDA     VAR1
            STA     M               update M with largest m
            JMP     1B
PRINT       LDA     SUM
            CHAR    0

* right shift rA:rX circularly by n; right shift rA by 1;
* store at OUTPUT buffer
            SRC     0,1
            STA     FRA
            STX     INT(1:4)
            ENT3    30,1            rI3 holds character code of n
            ST3     OUTPUT(2:2)
            OUT     OUTPUT(TERM)

            INC1    1
            CMP1    =5=
            JLE     LOOP
            HLT

            ORIG    1000
OUTPUT      ALF     "S1=  "
INT         ALF     "0000."
FRA         ALF     "00000"
            ORIG    *+BLOCK
            END     START
